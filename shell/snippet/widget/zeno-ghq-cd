#autoload

emulate -L zsh

local current_session dir options repository session
local -a out

out=( "${(f)^$(ghq list -p)}"(N-/) )
[[ -n $HOME ]] && out=( "${(@)out//#$HOME/~}" )

if [[ $#out == 0 ]]; then
  return 1
fi

options=${ZENO_ENABLE_FZF_TMUX:+${ZENO_FZF_TMUX_OPTIONS}}
options+=" --prompt='Project >' --preview 'cat \$(eval echo {})/README.md'"
options+=" --bind ctrl-d:preview-page-down,ctrl-u:preview-page-up"
dir=$(echo "${(F)out}" | eval "${ZENO_FZF_COMMAND} $options")

if [[ -z $dir ]]; then
  return 1
fi

BUFFER="cd ${dir// /\\ }"
zle accept-line

if [[ -n $TMUX ]]; then
  repository=${dir:t}
  session=${repository//./-}

  current_session=$(tmux list-sessions | grep 'attached' | cut -d":" -f1)
  if [[ $session -eq $current_session ]]; then
    tmux rename-session "${session}"
  fi
fi
