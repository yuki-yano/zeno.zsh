#autoload

emulate -L zsh

local callback command key options source_command
local -a out

out=( "${(f)$(zeno-call-client-and-fallback "completion" "${LBUFFER}")}" )

if [[ $out[1] != success ]]; then
  if whence fzf-completion > /dev/null; then
    zle fzf-completion
  else
    zle expand-or-complete
  fi
  return
fi

source_command=$out[2]
options=$out[3]
callback=$out[4]

command="${source_command} | \"\${ZENO_FZF_COMMAND}\" ${ZENO_FZF_TMUX_OPTIONS} ${options}"
out=( "${(f)$(eval $command)}" )

key=$out[1]
shift out

if [[ -n $callback ]]; then
  out=( "${(f)$(echo "${(F)out}" | eval $callback)}" )
fi

LBUFFER+=$out

zle zeno-snippet-next-placeholder
zle reset-prompt
