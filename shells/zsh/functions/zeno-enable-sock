#autoload

export ZENO_PID=

: ${ZENO_SOCK_DIR:="${XDG_RUNTIME_DIR:-${TMPDIR:-/tmp}}/zeno-${UID}"}
: ${ZENO_SOCK:="${ZENO_SOCK_DIR}/zeno-${$}.sock"}
export ZENO_SOCK_DIR
export ZENO_SOCK

function zeno-client() {
  local REPLY
  local -i isok fd
  local -a args
  zmodload zsh/net/socket
  zsocket ${ZENO_SOCK} >/dev/null 2>&1
  isok=$?
  (( isok == 0 )) || return 1
  fd=$REPLY
  # send as JSON
  args=( "${@//\\/\\\\}" )         # escape backslash
  args=( "${(@)args//$'\n'/\\n}" ) # escape new-line
  args=( "${(@)args//\"/\\\"}" )   # escape quote
  args=( "\"${(@)^args}\"" )       # quote
  printf '{"args":[%s]}' "${(j:,:)args}" >&$fd
  # receive result
  cat <&$fd
  exec {fd}>&-
}

function zeno-start-server() {
  local server_bin
  local -i i
  if [[ ! -d ${ZENO_SOCK:h} ]]; then
    mkdir -p "${ZENO_SOCK:h}"
  fi

  if [[ -e $ZENO_SOCK ]]; then
    rm -f "$ZENO_SOCK"
  fi

  server_bin="${ZENO_SERVER_BIN:-${ZENO_ROOT}/bin/zeno-server}"
  nohup "${server_bin}" >/dev/null 2>&1 &!

  for i in {1..50}; do
    if [[ -S $ZENO_SOCK ]]; then
      zeno-set-pid
      return 0
    fi
    sleep 0.1
  done

  print -u2 -- "zeno: failed to start socket server: $ZENO_SOCK"
  return 1
}

function zeno-restart-server() {
  zeno-stop-server
  zeno-start-server
}

function zeno-stop-server() {
  local pid
  local -i i
  zeno-set-pid
  pid="$ZENO_PID"

  if [[ -n $pid ]]; then
    kill "$pid" >/dev/null 2>&1
    for i in {1..20}; do
      if ! kill -0 "$pid" >/dev/null 2>&1; then
        break
      fi
      sleep 0.05
    done
    if kill -0 "$pid" >/dev/null 2>&1; then
      kill -KILL "$pid" >/dev/null 2>&1
    fi
  fi

  ZENO_PID=
  rm -f "$ZENO_SOCK"
}

function zeno-onchpwd() {
  zeno-client --zeno-mode=chdir --input.dir="$PWD"
}

function zeno-set-pid() {
  if [[ -z $ZENO_PID && -S $ZENO_SOCK ]]; then
    export ZENO_PID=$(zeno-client "--zeno-mode=pid")
  fi
}

autoload -Uz add-zsh-hook
add-zsh-hook precmd zeno-set-pid
add-zsh-hook chpwd zeno-onchpwd
add-zsh-hook zshexit zeno-stop-server

unfunction zeno-enable-sock
autoload -Uz zeno-enable-sock
