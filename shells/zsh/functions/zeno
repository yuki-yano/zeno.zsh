#autoload

local -a args deno_flags
# NOTE: Same workaround as bin/zeno: silence first-run dependency downloads to avoid wiping the prompt
local -i quiet=0

args=("$@")
deno_flags=(
  --unstable-byonm
  --no-check
  --allow-env
  --allow-read
  --allow-run
  --allow-write
  --allow-ffi
)

for (( i = 1; i <= $#args; ++i )); do
  case ${args[i]} in
    --zeno-mode=auto-snippet|--zeno-mode=completion)
      quiet=1
      break
      ;;
    --zeno-mode)
      if (( i + 1 <= $#args )); then
        case ${args[i+1]} in
          auto-snippet|completion)
            quiet=1
            break
            ;;
        esac
      fi
      ;;
  esac
done
(( quiet )) && deno_flags+=(--quiet)

command deno run "${deno_flags[@]}" -- "$ZENO_ROOT/src/cli.ts" "${args[@]}"
