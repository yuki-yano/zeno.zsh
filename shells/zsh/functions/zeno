#autoload

local -a args deno_flags
# NOTE: Same workaround as bin/zeno: silence first-run dependency downloads to avoid wiping the prompt
local -i quiet=0

args=("$@")
deno_flags=(
  --node-modules-dir=auto
  --no-check
  --allow-env
  --allow-read
  --allow-run
  --allow-write
  --allow-ffi
  --allow-net
)

for (( i = 1; i <= $#args; ++i )); do
  case ${args[i]} in
    --zeno-mode=auto-snippet|--zeno-mode=completion|--zeno-mode=completion-callback|--zeno-mode=completion-preview)
      quiet=1
      break
      ;;
    --zeno-mode)
      if (( i + 1 <= $#args )); then
        case ${args[i+1]} in
          auto-snippet|completion|completion-callback|completion-preview)
            quiet=1
            break
            ;;
        esac
      fi
      ;;
  esac
done
(( quiet )) && deno_flags+=(--quiet)

command deno run "${deno_flags[@]}" -- "$ZENO_ROOT/src/cli.ts" "${args[@]}"
