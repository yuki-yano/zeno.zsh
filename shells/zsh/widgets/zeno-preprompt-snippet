#autoload

emulate -L zsh

local in_zle=0
if (( $+BUFFER )); then
  in_zle=1
fi

local snippet lines options fzf_command
local -a out tmux_opts

fzf_command=${ZENO_FZF_COMMAND:-fzf}
if [[ $fzf_command == fzf-tmux ]]; then
  if [[ -n ${TMUX-} ]] && (( $+commands[fzf-tmux] )); then
    if [[ -n ${ZENO_FZF_TMUX_OPTIONS-} ]]; then
      tmux_opts=(${(z)ZENO_FZF_TMUX_OPTIONS})
    fi
  else
    fzf_command=fzf
  fi
fi

if [[ -z $1 ]]; then
  out=( "${(f)$(zeno-call-client-and-fallback --zeno-mode=snippet-list)}" )

  options=$out[1]
  shift out
  lines=${(F)out}

  local tmux_opts_str=""
  if (( ${#tmux_opts[@]} > 0 )); then
    tmux_opts_str="${(j: :)tmux_opts} "
  fi
  snippet=$(echo "$lines" | eval "${fzf_command} ${tmux_opts_str}${options}")
  snippet=${snippet%%:*}
else
  snippet=$1
fi

if [[ -z $snippet ]]; then
  return 0
fi

out=( "${(f)$(zeno-call-client-and-fallback --zeno-mode=preprompt-snippet \
  --input.snippet="$snippet" \
  )}" )

if [[ $out[1] == success && -n $out[2] ]]; then
  typeset -g ZENO_PREPROMPT_BUFFER="$out[2]"
  typeset -g ZENO_PREPROMPT_CURSOR="$out[3]"
  typeset -g ZENO_PREPROMPT_RAW="$snippet"

  if (( in_zle )); then
    BUFFER=$out[2]
    CURSOR=$out[3]
  fi
else
  unset ZENO_PREPROMPT_BUFFER ZENO_PREPROMPT_CURSOR ZENO_PREPROMPT_RAW
fi

(( in_zle )) && zle reset-prompt
return 0
