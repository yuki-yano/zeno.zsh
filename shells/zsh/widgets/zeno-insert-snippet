#autoload

emulate -L zsh

local lines options snippet fzf_command tmux_opts_str
local -a out tmux_opts

out=( "${(f)$(zeno-call-client-and-fallback --zeno-mode=snippet-list)}" )

options=$out[1]
shift out
lines=${(F)out}

fzf_command=${ZENO_FZF_COMMAND:-fzf}
if [[ $fzf_command == fzf-tmux ]]; then
  if [[ -n ${TMUX-} ]] && (( $+commands[fzf-tmux] )); then
    if [[ -n ${ZENO_FZF_TMUX_OPTIONS-} ]]; then
      tmux_opts=(${(z)ZENO_FZF_TMUX_OPTIONS})
    fi
  else
    fzf_command=fzf
  fi
fi

if (( ${#tmux_opts[@]} > 0 )); then
  tmux_opts_str="${(j: :)tmux_opts} "
else
  tmux_opts_str=""
fi

snippet=$(echo "$lines" | eval "${fzf_command} ${tmux_opts_str}${options}")
snippet=${snippet%%:*}

out=( "${(f)$(zeno-call-client-and-fallback --zeno-mode=insert-snippet \
  --input.lbuffer="$LBUFFER" \
  --input.rbuffer="$RBUFFER" \
  --input.snippet="$snippet" \
  )}" )

if [[ $out[1] == success && -n $out[2] ]]; then
  BUFFER=$out[2]
  CURSOR=$out[3]
fi

zle reset-prompt
