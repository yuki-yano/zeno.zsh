#!/usr/bin/env zsh

args=("$@")
deno_flags=(
  --unstable-byonm
  --no-check
  --allow-env
  --allow-read
  --allow-run
  --allow-write
  --allow-ffi
  --allow-net
)

# NOTE: Suppress --quiet for interactive modes because Deno's initial dependency downloads flood stderr,
#       which breaks the prompt and hides input until reset-prompt
quiet=0
for (( i = 1; i <= $#args; ++i )); do
  case ${args[i]} in
    --zeno-mode=auto-snippet|--zeno-mode=completion|--zeno-mode=completion-callback)
      quiet=1
      break
      ;;
    --zeno-mode)
      if (( i + 1 <= $#args )); then
        case ${args[i+1]} in
          auto-snippet|completion|completion-callback)
            quiet=1
            break
            ;;
        esac
      fi
      ;;
  esac
done
(( quiet )) && deno_flags+=(--quiet)

exec deno run "${deno_flags[@]}" -- "${ZENO_ROOT:-${0:a:h:h}}/src/cli.ts" "${args[@]}"
